apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

// idea borrowed from here: http://stackoverflow.com/questions/17097263/automatically-versioning-android-project-from-git-describe-with-android-studio-g

def getVersionCode = { ->
    def versionCode = -1

    try {
        def stdout = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'rev-list', '--first-parent', '--count', 'master'
            standardOutput = stdout
        }

        versionCode = Integer.parseInt(stdout.toString().trim())

        println "version code: " + versionCode

    }
    catch (ignored) { }

    return versionCode
}

def getVersionName = { ->
    def versionName = "noName"
    try {
        def stdout = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'describe', '--tags', '--dirty'
            standardOutput = stdout
        }

        versionName = stdout.toString().trim()

        println "version name: " + versionName
    }
    catch (ignored) { }

    return versionName
}

buildscript {
}


android {
    compileSdkVersion 25
    buildToolsVersion '25.0.2'
    defaultConfig {
        applicationId "org.dwallach.xstopwatchcomplication"
        minSdkVersion 25
        targetSdkVersion 25
        versionCode getVersionCode()
        versionName getVersionName()
        vectorDrawables.useSupportLibrary = true
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.google.android.support:wearable:2.0.0-beta2'
        provided 'com.google.android.wearable:wearable:1.0.0' // required per documentation
        // https://developer.android.com/training/wearables/apps/always-on.html

    compile 'com.google.android.gms:play-services-wearable:10.0.1'
//    compile 'com.android.support:palette-v7:24.0.0'
//    compile 'com.android.support:support-v13:24.0.0'
    compile 'com.android.support:appcompat-v7:25.1.1'
    compile 'com.android.support.constraint:constraint-layout:1.0.0-beta4' // because fun!

    compile "org.jetbrains.anko:anko-sdk23:$anko_version"
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
}
